#
# SPDX-License-Identifier: MIT
# Copyright (c) 2020 Tano Systems LLC. All rights reserved.
#

#@TYPE: Machine
#@NAME: AM335x ICE v2 EVM
#@DESCRIPTION: Machine configuration for the AM335x ICE v2 EVM sdcard

require conf/machine/include/am335x-evm.inc

#
# Machine/distro features
#
MACHINEOVERRIDES =. "am335x-icev2:"

MACHINE_FEATURES = "\
	apm vfat ext2 ext4 screen graphics \
	sgx ethernet cgroup lldp stp watchdog crypto \
	swupdate \
"

MACHINE_EXTRA_RDEPENDS += "igh-ethercat"

#
# Graphics
#
MACHINE_GUI_CLASS = "smallscreen"

#
# Kernel
#
ENABLE_TI_UIO_DEVICES ?= "0"

KERNEL_IMAGETYPE = "fitImage"
KERNEL_DEVICETREE = "am335x-icev2.dtb am335x-icev2-prueth.dtb"
KERNEL_DEVICETREE += "${@oe.utils.conditional('ENABLE_TI_UIO_DEVICES', '1', 'am335x-icev2-pru-excl-uio.dtb', '', d)}"
KERNEL_CLASSES = "kernel-tn-fitimage kernel-tn-mkimage"
KERNEL_MKIMAGE_TYPES = "ext4"

SERIAL_CONSOLES = "115200;ttyS0 115200;ttyS3"

# Use latest staging kernel
TANOWRT_HSL_TI_SDK_KERNEL ?= "0"

#
# Bootloader
#
UBOOT_DTB_LOADADDRESS  = "0x87e00000"
UBOOT_DTBO_LOADADDRESS = "0x87f00000"
UBOOT_DTBO_OFFSET      = "0x00010000"
UBOOT_RD_LOADADDRESS   = "0x84000000"
UBOOT_RD_ENTRYPOINT    = "0x84000000"

UBOOT_ENV_SIZE      = "0x20000"
UBOOT_ENV_REDUNDANT = "1"

UBOOT_MACHINE = ""
UBOOT_CONFIG = "sdcard-defconfig"
UBOOT_CONFIG[sdcard-defconfig] = "am335x_icev2_sdcard_defconfig,,u-boot.img"

UBOOT_SPL_MULTIPLE = "1"
UBOOT_SPL_MULTIPLE_COUNT = "3"
UBOOT_SPL_MULTIPLE_BLOCK_SIZE = "131072"

UBOOT_INITIAL_ENV = "u-boot-initial-env"

#
# Image
#
ROOTFS_IMAGE_FSTYPE = "squashfs-lzo"

IMAGE_FSTYPES = "${ROOTFS_IMAGE_FSTYPE} wic"
IMAGE_CLASSES += "wic-sdcard-img"

TANOWRT_ENABLE_OVERLAY_RESIZE = "1"

# WIC
WKS_FILE = "sdimage-ti-swu-a-b.wks.in"
WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"
WIC_DO_IMAGE_DEPENDS = "\
	${IMAGE_BASENAME}:do_image_squashfs_lzo \
	u-boot-startup-script:do_deploy \
	virtual/bootloader:do_deploy \
	virtual/kernel:do_deploy \
"
do_image_wic[depends] += "${WIC_DO_IMAGE_DEPENDS}"

# These variables are used in sdimage-ti-swu-a-b.wks.in
WKS_IMAGE_MLO_BINARY = "MLO-sdcard-defconfig"
WKS_IMAGE_UBOOT_BINARY = "u-boot-${MACHINE}.img-sdcard-defconfig"
WKS_IMAGE_UBOOT_INITIAL_ENV_BINARY = "u-boot-initial-env-${MACHINE}-sdcard-defconfig.bin"
WKS_IMAGE_STARTUP_BINARY = "startup-${MACHINE}.img"
WKS_IMAGE_KERNEL_BINARY = "fitImage-${MACHINE}.ext4"
WKS_IMAGE_ROOTFS_BINARY = "${IMAGE_BASENAME}-${MACHINE}.${ROOTFS_IMAGE_FSTYPE}"

PACKAGECONFIG_append_pn-fstools = " ovl-f2fs ovl-rootdisk-part "

IMAGE_FEATURES_remove = "modem"

#
# SWUPDATE
#
SWU_BOARD = "am335x-icev2-sd"
SWU_REVISION = "1.0"

SWU_BOOTLOADER = "uboot"
SWU_OVERLAY_CONTAINER = "blkdev"
SWU_OVERLAY_FS = "ext4"

require ${TANOWRT_HSL_BASE}/conf/machine/include/swu-ti-common.inc

SWU_IMAGE_ARTIFACT_spl[fname] = "MLO-sdcard-defconfig-x${UBOOT_SPL_MULTIPLE_COUNT}"
SWU_IMAGE_ARTIFACT_uboot[fname] = "u-boot-${MACHINE}"
SWU_IMAGE_ARTIFACT_uboot[ftype] = "img-sdcard-defconfig"

#
# OpenWrt specific
#
OPENWRT_VERSION_PRODUCT = "AM335x ICE EVM (SD)"
OPENWRT_VERSION_HWREV = "2.1"

#
# Other
#

# Refine the list of device targets for ti-pdk class recipes
TI_PDK_LIMIT_BOARDS = "icev2AM335x"
