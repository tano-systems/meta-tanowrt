#
# SPDX-License-Identifier: MIT
# Copyright (c) 2018-2020 Tano Systems LLC. All rights reserved.
#

#@TYPE: Machine
#@NAME: arm_versatile_926ejs
#@DESCRIPTION: Machine configuration for the ARMv5 system on QEMU

#
# CPU
#
require conf/machine/include/qemu.inc
require conf/machine/include/tune-arm926ejs.inc

require conf/machine/include/qemuall.inc

#
# PACKAGECONFIG's
#
PACKAGECONFIG_append_pn-fstools = " ovl-rootdisk-part "

#
# Image
#
IMAGE_FSTYPES = "wic"
IMAGE_CLASSES += "qemuarm-image-helper"
IMAGE_BOOT_FILES = "${KERNEL_IMAGETYPE} ${@parse_dtbs(d)}"

WKS_FILE = "tanowrt-image-qemuarm.wks"
WKS_FILE_DEPENDS = "e2fsprogs-native mtools-native squashfs-tools-native"
WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"

#
# Kernel
#
PREFERRED_PROVIDER_virtual/kernel_qemuarm = "linux-tano-yocto"
SERIAL_CONSOLES ?= "115200;ttyAMA0"
KERNEL_IMAGETYPE = "zImage"
KERNEL_DEVICETREE = "versatile-pb.dtb"

#
# Machine/distro features
#
MACHINE_FEATURES = "pci apm usbgadget usbhost vfat ext2 ethernet wifi cgroup stp lldp crypto"

#
# QEMU options (runqemu)
# ARM Versatile PB (on Cortex-A8)
#
QB_DEFAULT_FSTYPE = "wic"
QB_MACHINE = "-machine versatilepb"
QB_SYSTEM_NAME = "qemu-system-arm"
QB_KERNEL_CMDLINE_APPEND = "root=/dev/sda1 ro highres=off mem=512M console=ttyAMA0,115200 console=tty1"
QB_DTB = "${KERNEL_DEVICETREE}"
QB_SERIAL_OPT = "-serial stdio"

# Add the 'virtio-rng-pci' device otherwise the guest may run out of entropy
QB_RNG = "-object rng-random,filename=/dev/urandom,id=rng0 -device virtio-rng-pci,rng=rng0"

QB_OPT_APPEND = "\
	-usb \
	-nographic \
	-kernel ${DEPLOY_DIR_IMAGE}/${KERNEL_IMAGETYPE} \
	-dtb ${DEPLOY_DIR_IMAGE}/${KERNEL_DEVICETREE} \
	-append '${QB_KERNEL_CMDLINE_APPEND}' \
"

#
# OpenWrt specific
#
OPENWRT_VERSION_PRODUCT = "qemuarm"
OPENWRT_VERSION_HWREV = "v1"
